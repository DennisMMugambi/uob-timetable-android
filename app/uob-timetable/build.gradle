apply plugin: 'com.android.application'

def gitRevParse = { arg ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', arg, 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

android {
    compileSdkVersion 26
    buildToolsVersion "25.0.3"
    defaultConfig {
        applicationId "com.ak.uobtimetable"
        minSdkVersion 15
        targetSdkVersion 26
        versionCode 15
        versionName "2.4"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        buildConfigField "long", "TIMESTAMP", System.currentTimeMillis() + "L"
        buildConfigField "String", "GIT_COMMIT_HASH", "\"${gitRevParse("--short")}\""
        buildConfigField "String", "GIT_BRANCH", "\"${gitRevParse("--abbrev-ref")}\""
        buildConfigField "String", "BUGSNAG_KEY", "\"${System.getenv('UOB_TIMETABLE_BUGSNAG')}\""
    }
    buildTypes {
        debug {
            buildConfigField "boolean", "IS_DEBUG", "true"
        }
        release {
            minifyEnabled false
            buildConfigField "boolean", "IS_DEBUG", "false"
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:26.0.1'
    compile 'com.android.support:design:26.0.1'
    compile 'com.squareup.okhttp3:okhttp:3.7.0'
    compile 'com.google.code.gson:gson:2.8.0'
    compile 'com.bugsnag:bugsnag-android:4.0.0'
    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'com.h6ah4i.android.tablayouthelper:tablayouthelper:0.7.0'
    compile 'com.android.support:support-v4:26.0.1'
    compile 'com.android.support:recyclerview-v7:26.0.1'
    compile 'com.android.support:support-vector-drawable:26.0.1'
    testCompile 'junit:junit:4.12'
}
